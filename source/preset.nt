import {
  core.float.parse {parse-float},
  core.int.parse {parse-int},
  core.list {append},
  this.error {
    Argument-Missing,
    Key-Missing,
    Parse-Failure,
    Value-Missing,
  },
  this.parser {Parser, parser},
  this.util {split-at-key},
}

define flag(key: &text): parser(bool) {
  Parser of {
    reader = {
      function (xs: list(text)) {
        match split-at-key(key, xs) {
        | Error(ys) =>
          Pair(OK(False), ys)
        | OK(Pair(ys, zs)) =>
          Pair(OK(True), append(ys, zs))
        }
      }
    },
  }
}

define int-required(key: &text): parser(int) {
  Parser of {
    reader = {
      function (xs: list(text)) {
        match split-at-key(key, xs) {
        | Error(ys) =>
          Pair(Error(Key-Missing(*key)), ys)
        | OK(Pair(ys, zs)) =>
          match zs {
          | Nil =>
            Pair(Error(Value-Missing(*key)), ys)
          | Cons(value, rest) =>
            pin value = value in
            match parse-int(value) {
            | Error(_) =>
              Pair(Error(Parse-Failure(*key, *value)), ys)
            | OK(v) =>
              Pair(OK(v), append(ys, rest))
            }
          }
        }
      }
    },
  }
}

define float-required(key: &text): parser(float) {
  Parser of {
    reader = {
      function (xs: list(text)) {
        match split-at-key(key, xs) {
        | Error(ys) =>
          Pair(Error(Key-Missing(*key)), ys)
        | OK(Pair(ys, zs)) =>
          match zs {
          | Nil =>
            Pair(Error(Value-Missing(*key)), ys)
          | Cons(value, rest) =>
            pin value = value in
            match parse-float(value) {
            | Error(_) =>
              Pair(Error(Parse-Failure(*key, *value)), ys)
            | OK(v) =>
              Pair(OK(v), append(ys, rest))
            }
          }
        }
      }
    },
  }
}

define text-required(key: &text): parser(text) {
  Parser of {
    reader = {
      function (xs: list(text)) {
        match split-at-key(key, xs) {
        | Error(ys) =>
          Pair(Error(Key-Missing(*key)), ys)
        | OK(Pair(ys, Nil)) =>
          Pair(Error(Value-Missing(*key)), ys)
        | OK(Pair(ys, Cons(value, zs))) =>
          Pair(OK(value), append(ys, zs))
        }
      }
    },
  }
}

define text-argument(name: &text): parser(text) {
  Parser of {
    reader = {
      function (xs: list(text)) {
        match xs {
        | Nil =>
          Pair(Error(Argument-Missing(*name)), Nil)
        | Cons(y, ys) =>
          Pair(OK(y), ys)
        }
      }
    },
  }
}
